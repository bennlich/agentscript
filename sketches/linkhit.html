<html>
  <head>
    <title>Link Mouse Over Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../lib/mouse.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    # From Jim Lyons posting on Netlogo Users, via Steve Guerin
    u = ABM.util # shortcut

    class MyModel extends ABM.Model
      setup: -> # called by Model constructor
        @refreshPatches = @refreshAgents = false
        @mouse = new ABM.Mouse @
        
        @agents.setDefault "shape", "circle"; @agents.setDefault "size", .5
        p.sprout 1 for p in @patches.nOf 20
        @links.create a, @agents.other(a).oneOf() for a in @agents

      step: ->
        for l in @links
          l.color = if @mouseOnLink l then [256,0,0] else [128,128,128]

      mouseOnLink: (l) ->
        d = u.aSum (a.distanceXY @mouse.x, @mouse.y for a in l.bothEnds())
        d - l.length() < 1 / @patches.size

    model = new MyModel("layers", 25, -8, 8, -8, 8)
      .debug() # Debug: Put Model vars in global name space
      .start() # Run model immediately after startup initialization

    </script>
  </head>
  <body>
    <div id="layers"></div>
  </body>
</html>


