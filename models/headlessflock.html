<html>
  <head>
    <title>Headless Flock Model</title>
    <script src="../lib/agentscript.js"></script>
    <script src="../tools/coffee-script.js"></script>
    <script type="text/coffeescript">
    
    u = ABM.util # ABM.util alias, u.s is also ABM.shape accessor.
    class MyModel extends ABM.Model
      
      setVision: (@vision) -> @patches.cacheRect @vision, true
      setMaxTurn: (@maxTurn) -> @maxTurn = u.degToRad(@maxTurn)
      toggleConsole: () -> @useConsole = not @useConsole
      
      setup: ->
        # u.randomSeed() # use for repeatable runs
      
        @refreshPatches = false # <3fps->4fps agents larger part of drawing 
        # No optimizations: 4fps
        # @patches.usePixels() # 4fps same as refresh off
        @patches.cacheAgentsHere() # 17fps, big help for inRadius
        # w/ refresh off and two optimizations, 22-23fps
        @agents.setUseSprites() # 24 -> 36
        
        # Default is untimed, fastest for headless
        @anim.setRate 5, false # multistep
        
        @useConsole = true
        @population = 300     # agents
        @setVision 3          # patches & set patch rect
        @minSeparation = 0.75 # patches
        @setMaxTurn(3.0)      # degrees -> radians
      
        # defaults
        @agents.setDefault "size", 1.5
        # @agents.setDefault "color", [255,0,0] # slightly faster! ->38/39 ??

        p.color = u.randomGray 0, 100 for p in @patches
        p.sprout 1 for p in @patches.nOf(@population)
        a.color = u.randomMapColor() for a in @agents

        newDomEl = (agent) =>
          coords = @patches.patchXYtoPixelXY(agent.x, agent.y)
          el = document.body.appendChild(document.createElement("div"))
          el.innerHTML = 'bird'
          el.style.position = 'absolute'
          el.style.webkitTransform = 'translate('+coords[0]+'px, '+coords[1]+'px)'
          el.style.MozTransform = 'translate('+coords[0]+'px, '+coords[1]+'px)'
          return el

        @domElements = (newDomEl(agent) for agent in @agents)

  
      step: ->
        if @useConsole and @anim.ticks % 25 is 0
          console.log @anim.toString(), "flock alignment: #{@reportFlockVectorSize()}"
        @flock(a) for a in @agents
        if @anim.draws is 2 # Show the sprite sheet if there is one after first draw
          sheet = u.last(u.s.spriteSheets) if u.s.spriteSheets.length isnt 0
          if sheet?
            console.log sheet if @useConsole
            document.getElementById("play").appendChild(sheet.canvas)
        @stop() if @anim.ticks is 400

        @renderInDOM(a) for a in @agents

      renderInDOM: (agent) ->
        coords = @patches.patchXYtoPixelXY(agent.x, agent.y)
        el = @domElements[agent.id]
        el.style.webkitTransform = 'translate('+coords[0]+'px, '+coords[1]+'px)'
        el.style.MozTransform = 'translate('+coords[0]+'px, '+coords[1]+'px)'

      flock: (a) -> # a is agent
        flockmates = @agents.inRadius a, @vision
        if flockmates.length isnt 0
          [nearestNeighbor, d] = u.minOneOf flockmates, ((f) -> f.distance a), true
          if d < @minSeparation
            @separate a, nearestNeighbor
          else
            @align a, flockmates
            @cohere a, flockmates
        a.forward .5 # move partial patch for smooth animation.

      separate: (a, nearest) ->
        h = nearest.towards a; @turnTowards a, h
      align: (a, flockmates) ->
        @turnTowards a, @averageHeading(flockmates)
      cohere: (a, flockmates) ->
        @turnTowards a, @averageHeadingTowards(a, flockmates)
        
      turnTowards: (a, heading) ->
        turn = u.subtractRads heading, a.heading # angle from h to a
        turn = u.clamp turn, -@maxTurn, @maxTurn # limit the turn
        a.rotate turn
      averageHeading: (flockmates) ->
        dx = (Math.sin f.heading for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.heading for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      averageHeadingTowards: (a, flockmates) ->
        dx = (Math.sin f.towards a for f in flockmates).reduce (x,y) -> x+y
        dy = (Math.cos f.towards a for f in flockmates).reduce (x,y) -> x+y
        @safeAtan dy, dx
      safeAtan: (x,y) -> Math.atan2 y, x # if x is 0 and y is 0 then 0 else Math.atan2 y, x # atan ok if x 0
      
      reportFlockVectorSize: ->
        dx = (Math.sin a.heading for a in @agents).reduce (x,y) -> x+y
        dy = (Math.cos a.heading for a in @agents).reduce (x,y) -> x+y
        Math.sqrt(dx*dx + dy*dy)/@population

    # div, patchSize, minX, maxX, minY, maxY, isTorus, hasNeighbors, isHeadless
    #   Defaults: 13, -16, 16, -16, 16, false, true, false
    model = new MyModel({
      size: 15,
      minX: -16,
      maxX: 16,
      minY: -16,
      maxY: 16,
      isTorus: true,
      hasNeighbors: false
    })
    .debug() # Debug: Put Model vars in global name space
    .start() # Run model immediately after startup initialization

    </script>
  </head>
  <body>
    <div id="layers"></div>
    <div id="play" style="float:right;"></div>
  </body>
</html>